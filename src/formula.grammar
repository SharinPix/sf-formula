@top FormulaText { expr end }

expr { OrExpr }
OrExpr { AndExpr (OrOperator AndExpr)* }
AndExpr { CompExpr (AndOperator CompExpr)* }
CompExpr { AddExpr (CompOperator AddExpr)* }
AddExpr { MulExpr (AddOperator MulExpr)* }
MulExpr { Term (MulOperator Term)* }
Term { Number | String | Boolean | Function | parenExpr | Variable | Array }
parenExpr { "(" expr ")" }
Variable { VariableName ("." VariableAttribute)* }
VariableName { identifier }
VariableAttribute { identifier }
Function { FunctionName "(" (expr ("," expr)*)? ")" }
FunctionName { identifier }
Array { "[" (expr ("," expr)*)? "]" }
String { string }

@skip { space }

@tokens {
  space { @whitespace+ }
  Number { "-"? $[0-9]+ ("." $[0-9]+)? }
  Boolean { "true" | "false" }
  AddOperator { "+" | "-" | "&" }
  MulOperator { "*" | "/" }
  CompOperator { "<" | ">" | "==" | ">=" | "<=" | "!=" | "<>" | "=" }
  AndOperator { "&&" | "AND" }
  OrOperator { "||" | "OR" }
  identifier { $[A-Za-z_] $[A-Za-z0-9_]* }
  @precedence {Boolean, identifier}
  end { @eof }
  string { '"' !["]* '"' | "'" ![']* "'" }
}

@external propSource formulaHighlight from "./highlight"

@detectDelim